//// Grid
Nxy := 32
Nz := 1
SetGridSize(Nxy, Nxy, Nz)
SetCellSize(128e-9/Nxy, 128e-9/Nxy, 5e-9/Nz)
cellSize := 128e-9/Nxy

//// Material parameters
Msat = 800e3
Aex = 13e-12
print("Exchange length:", Sqrt((2*Aex.GetRegion(0))/(mu0*Pow(Msat.GetRegion(0), 2)))) // 5.6858e-9 so ok to use Nz = 1 for 5e-9 thickness

//// Track E_total, angle, shape
TableAdd(E_total)
TableAdd(E_zeeman)
roundness := 1.0
size := 100
TableAddVar(roundness, "Roundness", "")
TableAddVar(size, "Size", "nm")
TableAdd(Temp)
TableAdd(alpha)
TableAddVar(cellSize, "Cell_size", "m")
TableAutoSave(1e-11)
//AutoSave(m, 1e-9)

//// Temperature
alpha = 0.01 // If zero, temperature fluctuations are zero too
Temp = 300
Thermseed(12345)
setsolver(5)
fixdt = 0 // Default

//// Initial magnetization
m = Uniform(1, 0, 0)

//// Shape
geometry := Ellipse(size*1e-9, size*roundness*1e-9)
geometry = geometry.Add(geometry.RotZ(Pi/2))
SetGeom(geometry)
SaveAs(geom, sprintf("geomPlus%d_%.2f.ovf", size, roundness))

//// Run simulation
relax()
Run(1e-6)